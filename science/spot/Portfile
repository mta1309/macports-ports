PortSystem          1.0
PortGroup           cxx11 1.0
name                spot
version             2.3.3
categories          science math
maintainers         mmoll openmaintainer
description         Spot is an object-oriented model checking library written in C++.
long_description    ${description}
homepage            https://spot.lrde.epita.fr/index.html
platforms           darwin
license             GPL-3
master_sites        http://www.lrde.epita.fr/dload/spot/
checksums           md5     75d211b62ff9d7f0ece4aa37833f0728 \
                    sha1    a90a7e0486de80421f67478490d56a8abe8abcfa \
                    rmd160  70006d8a8ac84e9306b89cd11f030651490c0d62 \
                    sha256  1f2250c1039396bb4a18015d9c2b282a681cf617219ff4175b999589450eb13e
depends_lib-append  port:boost

configure.args-append --disable-python

set pythons_suffixes {34 35 36}

set pythons_ports {}
foreach s ${pythons_suffixes} {
    lappend pythons_ports python${s}
}

proc python_dir {} {
    global pythons_suffixes
    foreach s ${pythons_suffixes} {
        if {[variant_isset python${s}]} {
            set p python[string index ${s} 0].[string index ${s} 1]
            return [file normalize [exec ${p} -c "import sys; print(sys.prefix)"]/lib/${p}/site-packages]
        }
    }
    error "Python support not enabled."
}

foreach s ${pythons_suffixes} {
    set p python${s}
    set v [string index ${s} 0].[string index ${s} 1]
    set i [lsearch -exact ${pythons_ports} ${p}]
    set c [lreplace ${pythons_ports} ${i} ${i}]
    set packagepath ${destroot}${prefix}/Library/Frameworks/Python.framework/Versions/${v}/lib/python${v}
    eval [subst {
        variant ${p} description "Use Python ${v} for python bindings" conflicts ${c} {
            depends_lib-append      port:${p}
            configure.env-append    PYTHON=${prefix}/bin/python${v}
            configure.args-delete   --disable-python
            post-destroot {
                file mkdir ${destroot}${prefix}/Library/Frameworks/Python.framework/Versions/${v}/lib/python${v}
                move ${destroot}${prefix}/lib/python${v}/site-packages ${packagepath}/site-packages
            }
        }
    }]
}

# enable one python variant so that the python bindings can be compiled
set selected_python python35
foreach s ${pythons_suffixes} {
    if {[variant_isset python${s}]} {
        set selected_python python${s}
    }
}
default_variants +${selected_python}

livecheck.type  regex
livecheck.url   ${master_sites}
livecheck.regex ${name}-(\[0-9.\]+).tar.gz
